#
# Licensed to the Apache Software Foundation (ASF) under one
# or more contributor license agreements.  See the NOTICE file
# distributed with this work for additional information
# regarding copyright ownership.  The ASF licenses this file
# to you under the Apache License, Version 2.0 (the
# "License"); you may not use this file except in compliance
# with the License.  You may obtain a copy of the License at
#
#   http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing,
# software distributed under the License is distributed on an
# "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
# KIND, either express or implied.  See the License for the
# specific language governing permissions and limitations
# under the License.
#

define

relation publication @abstract,
  relates publisher,
  relates location,
  owns isn @key,
  owns title,
  owns genre,
  owns price,
  plays contribution:work,
  plays promotion-inclusion:item,
  plays order-line:item,
  plays review:reviewed,
  plays recommendation:recommended;

relation book @abstract,
  sub publication,
  owns isbn-13,
  owns isbn-10 @unique,
  owns year,
  owns page-count;

relation hardback sub book,
  owns stock;

relation paperback sub book,
  owns stock;

relation ebook sub book;

relation serial @abstract,
  sub publication,
  owns issn,
  owns next-issue-number,
  owns next-issue-date;

relation newspaper sub serial;

relation magazine sub serial;

relation journal sub serial;

relation music-score sub publication,
  owns ismn,
  owns year,
  owns page-count,
  owns stock;

entity contributor,
  owns name,
  plays contribution:contributor,
  plays authoring:author,
  plays editing:editor,
  plays illustrating:illustrator;

entity company @abstract,
  owns name;

entity publisher sub company,
  plays publication:publisher;

entity courier sub company,
  plays delivery:deliverer;

relation user,
  relates location,
  owns id @key,
  owns name,
  owns birth-date,
  plays user-action:user,
  plays recommendation:recipient;

relation user-action @abstract,
  relates user,
  owns timestamp;

relation order sub user-action,
  owns id @key,
  owns status,
  plays order-line:order,
  plays delivery:delivered;

relation review sub user-action,
  relates reviewed,
  owns id @key,
  owns score,
  owns verified;

relation login sub user-action,
  owns success;

entity promotion,
  owns code @key,
  owns name,
  owns start-timestamp,
  owns end-timestamp,
  plays promotion-inclusion:promotion;

relation address,
  relates location,
  owns street,
  plays delivery:destination;

relation place @abstract,
  owns name;

relation city sub place,
  relates location,
  plays publication:location,
  plays user:location,
  plays address:location;

relation state sub place,
  relates location,
  plays city:location;

relation country sub place,
  relates location,
  plays city:location,
  plays state:location;

entity world,
  plays country:location;

relation contribution,
  relates contributor,
  relates work;

relation authoring sub contribution,
  relates author as contributor;

relation editing sub contribution,
  relates editor as contributor;

relation illustrating sub contribution,
  relates illustrator as contributor;

relation promotion-inclusion,
  relates promotion,
  relates item,
  owns discount;

relation order-line,
  relates order,
  relates item,
  owns quantity,
  owns price;

relation delivery,
  relates deliverer,
  relates delivered,
  relates destination;

relation recommendation,
  relates recommended,
  relates recipient;

attribute isn @abstract, value string;
attribute isbn @abstract, sub isn;
attribute isbn-13 sub isbn;
attribute isbn-10 sub isbn;
attribute issn sub isn;
attribute ismn sub isn;
attribute title, value string;
attribute page-count, value integer;
attribute next-issue-number, value integer;
attribute next-issue-date, value datetime;
attribute genre, value string;
attribute stock, value integer;
attribute price, value double;
attribute discount, value double;
attribute id, value string;
attribute code, value string;
attribute name, value string;
attribute birth-date, value datetime;
attribute street, value string;
attribute year, value integer;
attribute quantity, value integer;
attribute score, value integer;
attribute verified, value boolean;
attribute timestamp, value datetime;
attribute start-timestamp, value datetime;
attribute end-timestamp, value datetime;
attribute status, value string @regex("^(paid|dispatched|delivered|returned|canceled)$");
attribute success, value boolean;
