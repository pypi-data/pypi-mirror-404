cmake_minimum_required(VERSION 3.14)
if(TARGET Prism::prism)
    return()
endif()
set(Prism_VERSION "@VERSION@")
set(Prism_FOUND TRUE)
get_filename_component(_prism_root "${CMAKE_CURRENT_LIST_DIR}/.." ABSOLUTE)
set(Prism_INCLUDE_DIRS "${_prism_root}/include")
if(ANDROID)
    set(_prism_abi "${ANDROID_ABI}")
elseif(CMAKE_ANDROID_ARCH_ABI)
    set(_prism_abi "${CMAKE_ANDROID_ARCH_ABI}")
elseif(CMAKE_SYSTEM_PROCESSOR MATCHES "aarch64|arm64")
    set(_prism_abi "arm64-v8a")
elseif(CMAKE_SYSTEM_PROCESSOR MATCHES "x86_64|amd64")
    set(_prism_abi "x86_64")
else()
    message(FATAL_ERROR "Prism: Cannot determine ABI. Set ANDROID_ABI.")
endif()
set(_prism_lib "${_prism_root}/lib/${_prism_abi}/libprism.so")
if(NOT EXISTS "${_prism_lib}")
    set(Prism_FOUND FALSE)
    if(Prism_FIND_REQUIRED)
        message(FATAL_ERROR "Prism: Library not found: ${_prism_lib}")
    endif()
    return()
endif()
set(Prism_LIBRARIES "${_prism_lib}")
add_library(Prism::prism SHARED IMPORTED)
set_target_properties(Prism::prism PROPERTIES
    IMPORTED_LOCATION "${_prism_lib}"
    INTERFACE_INCLUDE_DIRECTORIES "${Prism_INCLUDE_DIRS}")
